plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.gradle.plugin-publish' version '0.10.1'
}

apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'

version = '2.1.11'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    api 'com.wix:wix-embedded-mysql:4.6.1'
    api 'org.apache.commons:commons-lang3:3.9'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.14.0'
    testImplementation 'mysql:mysql-connector-java:8.0.18'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

pluginBundle {
    website = 'https://github.com/michaelruocco/embedded-mysql-plugin'
    vcsUrl = 'https://github.com/michaelruocco/embedded-mysql-plugin.git'
    description = 'Gradle plugin that enables running embedded MySQL for testing'
    tags = ['MySQL', 'embedded']
    version = project.version

    plugins {
        embeddedMysqlPlugin {
            id = 'com.github.michaelruocco.embedded-mysql-plugin'
            displayName = 'embedded-mysql-plugin'
        }
    }
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}